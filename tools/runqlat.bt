#!/usr/bin/env bpftrace
/*
 * runqlat.bt	CPU scheduler run queue latency as a histogram.
 *		For Linux, uses bpftrace, eBPF.
 *
 * This is a bpftrace version of the bcc tool of the same name.
 *
 * Copyright 2018 Netflix, Inc.
 *
 * 17-Sep-2018	Brendan Gregg	Created this.
 */

#ifndef BPFTRACE_HAVE_BTF
#include <linux/sched.h>
#else
/*
 * With BTF providing types, full headers are not needed.
 * We only need to supply the preprocessor defines used in this script.
 * TASK_RUNNING is not arch-dependant and has not changed in the linux
 * git history (it is not part of the stable API though)
 */
#define TASK_RUNNING 0
#endif

/*
 * Suppress warnings from the `delete` call, which is racy
 * because sched_switch gets called very very often.
 */
config = {
	log_warnings=0;
}

BEGIN
{
	printf("Tracing CPU scheduler... Hit Ctrl-C to end.\n");
}

tracepoint:sched:sched_wakeup,
tracepoint:sched:sched_wakeup_new
{
	@qtime[args.pid] = nsecs;
}

tracepoint:sched:sched_switch
{
	if (args.prev_state == TASK_RUNNING) {
		@qtime[args.prev_pid] = nsecs;
	}

	$ns = @qtime[args.next_pid];
	if ($ns) {
		@usecs = hist((nsecs - $ns) / 1000);
		delete(@qtime, args.next_pid);
	}
}

END
{
	clear(@qtime);
}
