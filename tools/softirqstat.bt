#!/usr/bin/bpftrace --unsafe
/**
 * softirqstat	Stat softirq like /proc/softirqs with better display.
 *				For Linux, uses bpftrace and eBPF.
 *
 * Written as a basic example of counting softirqs and printing a
 * per-second Statistic.
 *
 * USAGE: softirqstat.bt
 *
 * This is a bpftrace version of the bcc tool of the same name.
 *
 * Copyright 2022 CESTC, Inc.
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 * 24-Mar-2022	Rong Tao <rongtao@cestc.cn> Create this.
 */

#include <linux/interrupt.h>

BEGIN
{
	@softirqname[HI_SOFTIRQ] = "HI";
	@softirqname[TIMER_SOFTIRQ] = "TIMER";
	@softirqname[NET_TX_SOFTIRQ] = "NET_TX";
	@softirqname[NET_RX_SOFTIRQ] = "NET_RX";
	@softirqname[BLOCK_SOFTIRQ] = "BLOCK";
	@softirqname[IRQ_POLL_SOFTIRQ] = "IRQ_POLL";
	@softirqname[TASKLET_SOFTIRQ] = "TASKLET";
	@softirqname[SCHED_SOFTIRQ] = "SCHED";
	@softirqname[HRTIMER_SOFTIRQ] = "HRTIMER";
	@softirqname[RCU_SOFTIRQ] = "RCU";

	if ($# < 1) {
		printf("Input max CPU id.\n");
		exit();
	}
	printf("Tracing softirq_entry, hit ctrl+c to end.\n");
}

tracepoint:irq:softirq_entry
{
	$vec_nr = args->vec;

	@vec_count[cpu, @softirqname[$vec_nr]] += 1;
}

interval:s:1
{
	system("clear");
	printf("Statistic Softirq...\n");
	printf("\033[1;32m%-4s\033[m\033[1;33m %-10s %-10s %-10s %-10s %-10s ",
		"CPU", "HI", "TIMER", "NET_TX", "NET_RX", "BLOCK");
	printf("%-10s %-10s %-10s %-10s %-10s\033[m\n",
		"IRQ_POLL", "TASKLET", "SCHED", "HRTIMER", "RCU");

	$i = 0;
	unroll($1) {
	printf("%-4d %-10ld %-10d %-10d %-10d %-10d %-10d %-10d %-10d %-10d %-10d\n",
		$i,
		@vec_count[$i, "HI"],
		@vec_count[$i, "TIMER"],
		@vec_count[$i, "NET_TX"],
		@vec_count[$i, "NET_RX"],
		@vec_count[$i, "BLOCK"],
		@vec_count[$i, "IRQ_POLL"],
		@vec_count[$i, "TASKLET"],
		@vec_count[$i, "SCHED"],
		@vec_count[$i, "HRTIMER"],
		@vec_count[$i, "RCU"]);

		$i = $i + 1;
	}
}

END
{
	clear(@softirqname);
	clear(@vec_count);
}
